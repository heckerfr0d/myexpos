alias process_table_entry R4;
process_table_entry = PROCESS_TABLE + [SYSTEM_STATUS_TABLE + 1]*16;

// set MODE FLAG (in PROCESS TABLE) to syscall number
[process_table_entry + 9] = 9;

// store value of userSP in R0
alias userSP R0;
userSP = SP;

// switch to kernel stack
[process_table_entry + 13] = SP;
SP = [process_table_entry + 11]*512 - 1;

// get fileName from user stack
alias fileName R1;
fileName = [[PTBR + 2*(userSP-4)/512]*512 + (userSP-4)%512];

alias i R2;
i = INODE_TABLE + 1;
while (i < INODE_TABLE + 960) do
    if ([i]==fileName) then
        break;
    endif;
    i = i + 16;
endwhile;

if(i > INODE_TABLE + 960 || [i-1]!=EXEC) then
    // file invalid
    [[PTBR + 2*(userSP-1)/512]*512 + (userSP-1)%512] = -1;

    // restore SP to userSP
    SP = userSP;

    // reset MODE FLAG (in PROCESS TABLE) to 0
    [process_table_entry + 9] = 0;

    ireturn;
endif ;

i = (i - INODE_TABLE)/16;

// exit process
multipush(R0, R1, R2, R4);
R1 = 3;
R2 = [SYSTEM_STATUS_TABLE + 1];
call PROCESS_MANAGER;
multipop(R0, R1, R2, R4);

alias UAPage R3;
UAPage = [process_table_entry + 11];

// allocate same UAPage & PID
[MEMORY_FREE_LIST + UAPage] = [MEMORY_FREE_LIST + UAPage] + 1;
[SYSTEM_STATUS_TABLE + 2] = [SYSTEM_STATUS_TABLE + 2] - 1;

SP = UAPage*512 - 1;

[process_table_entry + 4] = RUNNING;
[process_table_entry + 7] = i;

// library
[PTBR+0] = 63;
[PTBR+1] = "0100";
[PTBR+2] = 64;
[PTBR+3] = "0100";

// get pages
multipush(R0, R1, R2, R3, R4);

// heap
R1 = 1;
R2 = [SYSTEM_STATUS_TABLE + 1];
call MEMORY_MANAGER;
[PTBR+4] = R0;
[PTBR+5] = "0110";
call MEMORY_MANAGER;
[PTBR+6] = R0;
[PTBR+7] = "0110";

// stack
call MEMORY_MANAGER;
[PTBR+16] = R0;
[PTBR+17] = "0110";
call MEMORY_MANAGER;
[PTBR+18] = R0;
[PTBR+19] = "0110";

multipop(R0, R1, R2, R3, R4);

alias codeSize R6;
codeSize = [INODE_TABLE + 16*i + 2];
if (codeSize%512 == 0) then
    codeSize = codeSize/512;
else
    codeSize = codeSize/512 + 1;
endif;

alias j R7;
j=0;

// code
while (j < codeSize) do
    multipush(R0, R1, R2, R3, R4, R6, R7);
    R1 = 1;
    R2 = [SYSTEM_STATUS_TABLE + 1];
    call MEMORY_MANAGER;
    multipop(R1, R2, R3, R4, R6, R7);
    [PTBR+8+j*2] = R0;
    loadi(R0, [INODE_TABLE + 16*i + 8 + j]);
    [PTBR+9+j*2] = "0100";
    multipop(R0);
    j = j + 1;
endwhile;

[[PTBR+16]*512] = [[PTBR+8]*512+1];

SP = 8*512;

[process_table_entry + 9] = 0;

ireturn;