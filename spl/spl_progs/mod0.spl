// RESOURCE_MANAGER
alias functionNum R1;
alias currentPID R2;

if (functionNum == ACQUIRE_BUFFER) then
    alias bno R3;
    alias bt R4;
    alias process_table_entry R5;
    process_table_entry = PROCESS_TABLE + 16*currentPID;
    bt = BUFFER_TABLE + bno*4;
    while ([bt+2]!=-1) do
        [process_table_entry + 4] = WAIT_BUFFER;
        [process_table_entry + 5] = bno;
        multipush(R1, R2, R3, R4, R5);
        R1 = RELEASE_LOCK;
        R2 = KERN_LOCK;
        call ACCESS_CONTROL;

        call SCHEDULER;

        R1 = ACQUIRE_KERN_LOCK;
        call ACCESS_CONTROL;
        multipop(R1, R2, R3, R4, R5);
    endwhile;
    [bt+2] = currentPID;
    return;
endif;

if (functionNum == RELEASE_BUFFER) then
    alias bno R3;
    alias bt R4;
    bt = BUFFER_TABLE + bno*4;
    if ([bt+2]!=currentPID) then
        R0 = -1;
    else
        [bt+2] = -1;
        alias i R5;
        i = PROCESS_TABLE + 20;
        while (i<PROCESS_TABLE+256) do
            if ([i]==WAIT_BUFFER && [i+1]==bno) then
                [i] = READY;
            endif;
            i = i + 16;
        endwhile;
        R0 = 0;
    endif;
    return;
endif;

if(functionNum == ACQUIRE_DISK) then
    while([DISK_STATUS_TABLE] == 1) do
        [PROCESS_TABLE + 16*currentPID + 4] = WAIT_DISK;
        multipush(R1,R2);
        R1 = RELEASE_LOCK;
        R2 = KERN_LOCK;
        call ACCESS_CONTROL;

        call SCHEDULER;

        R1 = ACQUIRE_KERN_LOCK;
        call ACCESS_CONTROL;
        multipop(R1,R2);
    endwhile;
    [DISK_STATUS_TABLE] = 1;
    [DISK_STATUS_TABLE + 4] = currentPID;
    return;
endif;

if(functionNum == ACQUIRE_TERMINAL) then
    while([TERMINAL_STATUS_TABLE] == 1) do
        [PROCESS_TABLE + 16*currentPID + 4]=WAIT_TERMINAL;
        multipush(R1,R2);

        R1 = RELEASE_LOCK;
        R2 = KERN_LOCK;
        call ACCESS_CONTROL;

        call SCHEDULER;

        R1 = ACQUIRE_KERN_LOCK;
        call ACCESS_CONTROL;

        multipop(R1,R2);
    endwhile;
    [TERMINAL_STATUS_TABLE] = 1;
    [TERMINAL_STATUS_TABLE + 1] = currentPID;
    return;
endif;

if(functionNum == RELEASE_TERMINAL) then
    if(currentPID == [TERMINAL_STATUS_TABLE+1]) then
        [TERMINAL_STATUS_TABLE]=0;
        alias i R3;
        i = PROCESS_TABLE + 20;
        while(i < PROCESS_TABLE+256) do
            if([i] == WAIT_TERMINAL) then
                [i] = READY;
            endif;
            i=i+16;
        endwhile;
        R0=0;
        return;
    else
        R0=-1;
        return;
    endif;

endif;

if (functionNum == ACQUIRE_INODE) then
    alias fid R3;
    alias fst R4;
    fst = FILE_STATUS_TABLE + fid*4;
    while([fst] != -1) do
        [PROCESS_TABLE + 16*currentPID + 4] = WAIT_FILE;
        [PROCESS_TABLE + 16*currentPID + 5] = fid;
        multipush(R1, R2, R3, R4);
        R1 = RELEASE_LOCK;
        R2 = KERN_LOCK;
        call ACCESS_CONTROL;

        call SCHEDULER;

        R1 = ACQUIRE_KERN_LOCK;
        call ACCESS_CONTROL;
        multipop(R1, R2, R3, R4);
    endwhile;
    if ([INODE_TABLE + fid*16 + 1]==-1) then
        R0 = -1;
        return;
    endif;
    [fst] = currentPID;
    R0 = 0;
    return;
endif;

if (functionNum == RELEASE_INODE) then
    // release inode
    alias fid R3;
    alias fst R4;
    fst = FILE_STATUS_TABLE + fid*4;
    if ([fst]!=currentPID) then
        R0 = -1;
        return;
    endif;

    [fst] = -1;
    alias i R5;
    i = PROCESS_TABLE + 36;
    while (i<PROCESS_TABLE+240) do
        if ([i]==WAIT_FILE && [i+1]==fid) then
            [i] = READY;
        endif;
        i = i + 16;
    endwhile;
    R0 = 0;
    return;
endif;

if (functionNum == 6) then
    // acquire semaphore
    alias i R0;
    alias n R3;
    i = SEMAPHORE_TABLE + 1;
    n = SEMAPHORE_TABLE + 128;
    while (i < n) do
        if ([i]==0) then
            [i] = 1;
            [i-1] = -1;
            R0 = (i - SEMAPHORE_TABLE - 1)/4;
            return;
        endif;
        i = i + 4;
    endwhile;
    R0 = -1;
    return;
endif;

if (functionNum == 7) then
    // release semaphore
    alias semid R3;
    alias sem_entry R4;
    sem_entry = SEMAPHORE_TABLE + semid*4;
    if ([sem_entry]==currentPID) then
        [sem_entry] = -1;
        alias i R5;
        i = PROCESS_TABLE + 20;
        while (i<PROCESS_TABLE+256) do
            if ([i]==WAIT_SEMAPHORE && [i+1]==semid) then
                [i] = READY;
            endif;
            i = i + 16;
        endwhile;
    else
        if ([sem_entry]!=-1 || [sem_entry+1]==0) then
            R0 = -1;
            return;
        endif;
    endif;
    [sem_entry+1] = [sem_entry+1] - 1;
    R0 = 0;
    return;
endif;
